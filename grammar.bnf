// Protocol definition grammar in BNF.
<File>          ::= Doc? 'module' Identifier ';' Import* Definition*

// Doc string.
<Doc>           ::= '/**' '...text...' '*/'

// Imports can be absolute and relative to an absolute module.
<Import>        ::= AbsImport | RelImport
<AbsImport>     ::= 'import' Identifier ';'
<RelImport>     ::= 'from' Identifier 'import' Identifier (',' Identifier)* ';'

<Definition>    ::= Doc? (Enum | Message | Interface)

<Enum>          ::= 'enum' Identifier '{' EnumValues '}'
<EnumValues>    :: = EnumValue (',' EnumValue)* ';'?
<EnumValue>     :: = Doc? Identifier

<Message>       ::= ('message' | 'exception') Identifier (':' MessageBase)? '{' Fields? '}'
<MessageBase>   ::= DefType | (DefType '(' DefType ')')

<Fields>        ::= Field (';' Field)* ';'?
<Field>         ::= Doc? Identifier Type (',' FieldOptions)?
<FieldOptions>  ::= FieldOption (',' FieldOption)*
<FieldOption>   ::= 'discriminator' | 'deprecated'

<Interface>     ::= 'interface' Identifier (':' InterfaceOptions)? '{' Method* '}'
<InterfaceOptions>  := (InterfaceBase (',' InterfaceExc)?) | InterfaceExc?
<InterfaceBase>     := DefType
<InterfaceExc>      := 'throws' DefType

<Method>        ::= Doc? Identifier '(' Fields? ')' Type (',' MethodOptions)? ';'
<MethodOptions> ::= MethodOption (',' MethodOption)*
<MethodOption>  ::= 'mutator' | 'deprecated' | 'index'

<Type>          ::= ValueType | ListType | SetType | MapType | DefType
<ValueType>     ::= 'bool'
                    | 'int16'
                    | 'int32
                    | 'int64'
                    | 'float'
                    | 'double'
                    | 'string'
                    | 'object'
                    | 'void'
<ListType>      ::= 'list' '<' Type '>'
<SetType>       ::= 'set' '<' Type '>'
<MapType>       ::= 'map' '<' Type ',' Type '>'
<DefType>       ::= Identifier

<Identifier>    ::= ( Letter | '_' ) ( Letter | Digit | '.' | '_' )*
<Letter>        ::= ['A'-'Z'] | ['a'-'z']
<Digit>         ::= ['0'-'9']
